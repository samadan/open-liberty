/*******************************************************************************
 * Copyright (c) 2017, 2025 IBM Corporation and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License 2.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-2.0/
 * 
 * SPDX-License-Identifier: EPL-2.0
 *
 * Contributors:
 *     IBM Corporation - initial API and implementation
 *******************************************************************************/

configurations {
  requiredLibs {
    transitive = false
  }
}

dependencies { 
  requiredLibs project(':com.ibm.ws.security.registry'),
    project(':com.ibm.ws.security.registry_test.servlet'),
    'com.unboundid:unboundid-ldapsdk:5.1.0',
    project(':com.ibm.ws.com.unboundid')
}
jar.dependsOn ':com.ibm.ws.security.registry_test.servlet:assemble'

autoFVT.dependsOn ':com.ibm.ws.security.registry_test.servlet:assemble'
autoFVT.dependsOn ':com.ibm.ws.security.wim.repository_test.custom:assemble'
addRequiredLibraries.dependsOn addJakartaTransformer

autoFVT {
  def repositoryTestProject = project(':com.ibm.ws.security.wim.repository_test.custom')
  def registryServletProject = project(':com.ibm.ws.security.registry_test.servlet')
  doLast {
  /*
   * Copy sample CustomRepository user feature to this test bucket.
   */
  copy { 
    from repositoryTestProject.file('publish/usr/extension/lib/features/customRepositorySample-1.0.mf')
    into new File(autoFvtDir, 'publish/features')
  }
  copy {
    from new File(repositoryTestProject.buildDir, 'com.ibm.ws.security.wim.repository_test.custom.jar')
    into new File(autoFvtDir, 'publish/bundles')
    rename 'com.ibm.ws.security.wim.repository_test.custom.jar', 'com.ibm.ws.security.wim.repository_test.custom_1.0.jar'
  }

  /*
   * Copy the userRegistry WAR into test servers.
   */
  def servers = [
    'com.ibm.ws.security.wim.core.fat.invalidBaseEntryInRealm',
    'com.ibm.ws.security.wim.core.fat.maxSearchResult',
    'com.ibm.ws.security.wim.core.fat.searchTimeOut',
    'com.ibm.ws.security.wim.core.fat.dynamic',
    'com.ibm.ws.security.wim.core.fat.noreg',
    'com.ibm.ws.security.wim.core.fat.WimCoreRegression'
  ]
  servers.each { server ->
    copy {
      from new File(registryServletProject.buildDir, 'test-application')
      into new File(autoFvtDir, 'publish/servers/' + server + '/dropins')
      include '*.war'
    }
  }
  }
}  
