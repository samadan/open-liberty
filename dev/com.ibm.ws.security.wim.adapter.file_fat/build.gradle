/*******************************************************************************
 * Copyright (c) 2017, 2025 IBM Corporation and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License 2.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-2.0/
 *
 * SPDX-License-Identifier: EPL-2.0
 *
 * Contributors:
 *     IBM Corporation - initial API and implementation
 *******************************************************************************/

dependencies {
  requiredLibs project(':com.ibm.ws.security.registry'),
    project(':com.ibm.ws.security.registry_test.servlet')
}
autoFVT.dependsOn ':com.ibm.ws.security.registry_test.servlet:assemble'
autoFVT {
  def adapterFileProject = project(':com.ibm.ws.security.wim.adapter.file')
  def apacheDirectoryProject = project(':com.ibm.ws.org.apache.directory.server')
  def registryServletProject = project(':com.ibm.ws.security.registry_test.servlet')
  doLast {
  /* 
   * Copy test.file.adapter bundle to autoFVT files so that it can later be copied to Liberty root 
   * during test execution. 
   */
  copy {
    from new File(adapterFileProject.buildDir, 'com.ibm.ws.security.wim.adapter.file.jar')
    into new File(autoFvtDir, 'lib/LibertyFATTestFiles')
    rename 'com.ibm.ws.security.wim.adapter.file.jar', 'com.ibm.ws.security.wim.adapter.file_1.0.jar'
  }

  /*
   * Copy the local ApacheDS LDAP instances. 
   */
  copy {
    from apacheDirectoryProject.projectDir
    into autoFvtDir
    include 'apacheds-2.0.0-M15/**'
  }

  /*
   * Copy the userRegistry WAR into test servers.
   */
  def servers = [
    'com.ibm.ws.security.wim.adapter.file.fat.allURAPIs',
    'com.ibm.ws.security.wim.adapter.file.fat.multiplerepos',
    'com.ibm.ws.security.wim.adapter.file.fat.repowithoutid'
  ]
  servers.each { server ->
    copy { 
      from new File(registryServletProject.buildDir, 'test-application')
      into new File(autoFvtDir, 'publish/servers/' + server + '/dropins')
      include '*.war'
    }
  }
  }
}
